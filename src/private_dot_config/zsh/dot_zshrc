# vim: set ft=zsh:

source_if_exists() {
  local file_to_source="$1"
  if [ -f "$file_to_source" ]; then
    # shellcheck disable=SC1090
    source "$file_to_source"
  else
    local current_script_file
    current_script_file=$(basename "$0")
    echo "[WARN][$current_script_file] File '$file_to_source' does not exist."
  fi
}

# load zsh-defer
source_if_exists $ZDOTDIR/plugins/manual/zsh-defer/zsh-defer.plugin.zsh

# load configs
for f ($ZDOTDIR/sync/*(N-.)) source_if_exists $f
for f ($ZDOTDIR/defer/*(N-.)) zsh-defer source_if_exists $f

# load plugins
for f ($ZDOTDIR/plugins/autoload/*/*.plugin.zsh(N-.)) zsh-defer source_if_exists $f

# load depend configs
# zsh-defer source_if_exists $ZDOTDIR/depend/setup-abbr-fast-syntax-highlighting.zsh

autoload -Uz compinit
if [[ -n "${GITHUB_ACTIONS}" ]]; then
  compinit -u
else
  compinit
fi
